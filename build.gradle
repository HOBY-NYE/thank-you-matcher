plugins {
    id 'application'
    id 'jacoco'
    id 'pmd'

    id 'org.sonarqube' version '2.8'
    id 'com.github.spotbugs' version '3.0.0'
//    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group       = 'org.hoby.nye.tym'
version     = '1.1.0'
description = 'Creates an Excel spreadsheet with information about who a student should write a thank you letter to.'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    implementation 'org.apache.poi:poi-ooxml:3.17'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

wrapper {
    gradleVersion    = '6.1.1'
    distributionType = Wrapper.DistributionType.ALL
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'Thank You Matcher'
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestCoverageVerification {
    violationRules {
        // Ensure a minimum of 80% of the project is covered by tests
        rule {
            limit {
                counter = 'LINE'
                value   = 'COVEREDRATIO'
                minimum = 0.0
            }
        }

        // Ensure all branch points are covered
        rule {
            limit {
                counter = 'BRANCH'
                value   = 'MISSEDCOUNT'
                maximum = 100000
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}

application {
    mainClassName = 'org.hoby.nye.tym.ThankYouMatcher'
}

tasks.withType(JavaCompile) {
    options.encoding    = 'UTF-8'
    options.compilerArgs += ['-Xlint:deprecation']
}
